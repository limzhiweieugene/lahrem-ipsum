#lang web-server/insta
 (require racket/trace)

; render-paragraphs: paragraphs -> xexpr
; Based on number of paragraphs requested, produces a list of xexpr fragments of paragraphs.
(define (render-paragraphs n)
  (if (= n 0)
      '()
      (cons (render-paragraph) (render-paragraphs (- n 1)))))

; render-paragraphs: paragraph -> xexpr
; Produces an xexpr fragment of the paragraph with randomly selected number of sentences
; within the range.
(define (render-paragraph)
  `(p ,(generate-paragraph (random 15 25))))

; Capitalizes the first letter in a setnence.
(define (capitalize str)
  (string-set! str 0 (char-upcase (string-ref str 0)))
  str)

; Generates a paragraph string consisting of the given number of sentences.
; Each sentence consists of a random number of words within the range.
(define (generate-paragraph n)
  (let ([sentence (capitalize (generate-sentence (random 5 10) singlish-dictionary))])
    (if (= n 1)
        sentence
        (string-append sentence " " (generate-paragraph (- n 1))))))

; Generates a sentence based on the given number of words.
; Words chosen at random from the dictionary.
(define (generate-sentence n lst)
  (if (= n 1)
      (string-append (list-ref lst (random (length lst))) ".")
      (string-append (list-ref lst (random (length lst))) " " (generate-sentence (- n 1) lst))))

; render-paragraphs: words -> xexpr
; Based on number of words requested, produces a list of xexpr fragments of words.
(define (render-words n)
  (list `(p ,(generate-words n))))

; Recursively selects for the right number of words in sentences.
(define (generate-words n)
  (let ([current-length (random 5 10)])
    (if (<= n 10)
        (capitalize (generate-sentence n singlish-dictionary))
        (string-append (capitalize (generate-sentence current-length singlish-dictionary)) " " (generate-words (- n current-length))))))

; Singlish dictionary as a list of a strings
(define singlish-dictionary
  (list
   "lor"
   "lah"
   "leh"
   "walao"
   "sian"
   "kambing"
   "sayang"
   "liao"
   "liddat"
   "siao"
   "abuden"
   "abit"
   "action"
   "agak-agak"
   "ahbeng"
   "ahlian"
   "aiyo"
   "alamak"
   "angmoh"
   "arrow"
   "atas"
   "auntie"
   "belanja"
   "blur"
   "bodoh"
   "bochup"
   "bojio"
   "boliao"
   "bopian"
   ))

; start: request -> response
; Consumes a request and produces a page that displays all of the
; web content.
(define (start request)
  (define output
    (cond
      [(can-parse-input? (request-bindings request))
           (parse-input (request-bindings request))]
      [else '(`(p ,"Generate 1-100 paragraphs or 1-25000 words."))]))
  (render-page output request))


; can-parse-post?: bindings -> boolean
; Produces true if bindings contains values for 'title and 'body.
; Checks if bindings are correct input and format
(define (can-parse-input? bindings)
  (and (exists-binding? 'type bindings)
       (exists-binding? 'length bindings)
       (exact-positive-integer? (string->number (extract-binding/single 'length bindings)))
       (or (and
            (equal? (extract-binding/single 'type bindings) "paragraph")
            (<= (string->number (extract-binding/single 'length bindings)) 100))
           (and
            (equal? (extract-binding/single 'type bindings) "word")
            (<= (string->number (extract-binding/single 'length bindings)) 25000)))))

; parse-post: bindings -> post
; Consumes a bindings, and produces a post out of the bindings.
(define (parse-input bindings)
  (if (equal? (extract-binding/single 'type bindings) "paragraph")
      (render-paragraphs (string->number (extract-binding/single 'length bindings)))
      (render-words (string->number (extract-binding/single 'length bindings)))))

; render-blog-page: blog request -> response
; Consumes a blog and a request, and produces an HTML page
; of the content of the blog.
(define (render-page output request)
  (response/xexpr
   `(html (head (title "Lohrem Ipsum")
                (link ((rel "stylesheet")
                       (href "/stylesheet.css")
                       (type "text/css"))))
          (body (h1 "Lohrem Ipsum")
                (div ((class "input"))
                     (form ((id "parameters"))
                      (input ((name "length")))
                      (select ((form "parameters") (name "type"))
                             (option ((value "paragraph")) "Paragraphs")
                             (option ((value "word")) "Words"))
                      (input ((type "submit") (value "Generate"))))
                (div ((class "output"))
                     ,@output))))))

(static-files-path "htdocs")
